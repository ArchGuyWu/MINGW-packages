_realname=wasi-libc
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0+389+3f43ea9a
_commit=3f43ea9abb24ed8d24d760989e1d87ea385f8eaa  # tags/wasi-sdk-23
pkgrel=1
epoch=1
pkgdesc="WASI libc implementation for WebAssembly (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/WebAssembly/wasi-libc"
msys2_references=(
  'archlinux: wasi-libc'
)
license=('Apache-2.0 WITH LLVM-exception AND Apache-2.0 AND MIT')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-clang"
  'git'
  "${MINGW_PACKAGE_PREFIX}-llvm"
)
options=('staticlibs')
source=("git+$url.git#commit=${_commit}")
b2sums=('8d011e0b4b5abe80dfbc312b50a9f9fc8799d2443bab12cc24ddafbd582c0bf30638d38a9c9e167626f0b3c2f0e11adfc625999543f95a32c48a00a70ac664df')

prepare() {
  cd "${_realname}"

  printf "0+%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_realname}"

  local target= make_options=(
    CC=${MINGW_PREFIX}/bin/clang
    AR=${MINGW_PREFIX}/bin/llvm-ar
    NM=${MINGW_PREFIX}/bin/llvm-nm
  )

  local -A targets=(
    wasm32-wasi           ''
    wasm32-wasip1         ''
    wasm32-wasip1-threads 'THREAD_MODEL=posix'
    wasm32-wasip2         'WASI_SNAPSHOT=p2'
  )

  for target in "${!targets[@]}"; do
    make "${make_options[@]}" TARGET_TRIPLE="$target" ${targets[$target]}
  done
}

package() {
  cd "${_realname}"

  install -dm755 "${pkgdir}${MINGW_PREFIX}/share"

  cp -dr --preserve=mode,timestamp sysroot "${pkgdir}${MINGW_PREFIX}/share/wasi-sysroot"

  # Package licenses
  install -Dm644 LICENSE* -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
